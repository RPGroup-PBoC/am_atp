// Upload image from stack to SLM before each frame in MDA.
//
// We have to use the core API to upload each frame individually,
// since there is no public sequencing API for SLM devices.
//
// Pariksheet Nanda <[hidden email]> July 2014
// Edited by Tyler Ross 10/15/2018
// Edited by Catherine Ji 06/27/2022
// License: Public Domain

import ij.io.Opener; // To load TIFF stack file from disk.
import ij.ImagePlus; // To hold the opened image.
import ij.ImageStack; // To access stack pixels.

// Load file from disk.
Opener opener = new Opener();
ImagePlus imp =
opener.openImage("C:/Users/Aster/Documents/6_27_22_Circle.tif"); // "/path/to/stack.tif"

// Get stack info.
ImageStack stack = imp.getImageStack();
slices = stack.getSize();

// Set MDA to acquire the number of slices.
//seqSettings = gui.getAcquisitionSettings();
//seqSettings.numFrames = slices;
//gui.setAcquisitionSettings(seqSettings);

// Get the installed name of the SLM.
mosaic = mmc.getSLMDevice();

// Boilerplate when using runnables.
acq.clearRunnables();

// Runnable to upload each image to the SLM.
runnable = new Runnable() {
      int roi = 1;
      int npos = 3;
      int globalCount = 0;

      int lag = 3;
      int lag_temp = 0;

      int pos = 0;
      int initLength = 10; // cycles

		//Initialize state
      print("Script Loaded");
      public void run() {
      
         pos = globalCount%npos;

         // Get the pixels of the stack slice.
         pixels = stack.getPixels(roi);
         // Upload the image to the SLM.
         mmc.setSLMImage(mosaic, pixels);
         // Activate the uploaded image on the SLM.
         mmc.displaySLMImage(mosaic);
         print("Activated slice " + roi);

// initLength * npos
        if ((globalCount >= (initLength * npos)) && (pos == 0)){
            if (lag_temp == (lag - 1)){
               roi = roi + 1;
            }
        }
         
         globalCount = globalCount + 1;
         lag_temp = (lag_temp + 1)%lag;
      }
   };

// Dimension order is frame, position, channel, slice.
acq.attachRunnable(-1, -1, 0, 0,runnable); 
